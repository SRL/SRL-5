(*
Animation
=========


.. contents::


*)

(*
PixelShiftMulti
~~~~~~~~~~~~~~~

.. code-block:: pascal

    function PixelShiftMulti(B: TBoxArray; T: Integer): TIntegerArray;

Calculates how many pixels change in boxes B during time T.

.. note::

    Author: marpis
    Last Modified: Unknown

Example:

.. code-block:: pascal

    PixelShiftArray := PixelShiftMulti(box, 5000);
*)
function PixelShiftMulti(B: TBoxArray; T: Integer): TIntegerArray;
Var
  L, I: Integer;
  BMP, BMP2: TIntegerArray;
begin
  L := Length(B);
  SetLength(BMP, L);
  SetLength(BMP2, L);
  SetLength(Result, L);

  for I := 0 to (L - 1) do
    BMP[I] := BitmapFromClient(B[i].x1,B[i].y1,B[i].x2,B[i].y2);
  Wait(T);
  for I := 0 to (L - 1) do
    BMP2[I] := BitmapFromClient(B[i].x1,B[i].y1,B[i].x2,B[i].y2);
  for i := 0 to (L - 1) do
    result[i] := CalculatePixelShift(BMP[i],BMP2[i],IntToBox(0,0,(b[i].x2 - b[i].x1),(b[i].y2-b[i].y1)));
  for I := 0 to (L - 1) do
  begin
    FreeBitmap(BMP[I]);
    FreeBitmap(BMP2[I]);
  end;
end;

(*
PixelShift
~~~~~~~~~~

.. code-block:: pascal

    function PixelShift(B: TBox; T: Integer): Integer;

Calculates how many pixels change in boxes B during time T.

.. note::

    Author: marpis
    Last Modified: Nov. 29, 2009

Example:

.. code-block:: pascal

    Result := (PixelShift(box, 5000) > 2000);
*)
function PixelShift(B: TBox; Time: Integer): Integer;
var
  TIA: TIntegerArray;
begin
  TIA := PixelShiftMulti([B], Time);
  Result := TIA[0];
end;

(*
AveragePixelShift
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function AveragePixelShift(B: TBox; waitPerloop, maxTime: Integer): Integer;

Gets the average pixel shift every 'waitPerLoop' ms over the time 'maxTime'

.. note::

    Author: Coh3n
    Last Modified: Mar. 31st, 2011

Example:

.. code-block:: pascal

    Result := (AveragePixelShift(box, 500, 5000) > 2000);
*)
function AveragePixelShift(b: TBox; waitPerLoop, maxTime: integer): integer;
var
  t: integer;
  shifts: TIntegerArray;
begin
  t := (getSystemTime + maxTime);

  while (getSystemTime < t) do
  begin
    setLength(shifts, length(shifts)+1);
    shifts[high(shifts)] := pixelShift(b, waitPerLoop);
  end;

  result := averageTIA(shifts);
end;

(*
ReturnGridColorsInBox
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function ReturnGridColorsInBox(Box : TBox): T2DIntegerArray;

Returns a 2D color array from the pixels contained by Box.

.. note::

    Author: m34tcode
    Last Modified: 4/7/2012
*)
function ReturnGridColorsInBox (Box : TBox) : T2DIntegerArray;
var
  x,y : Integer;
begin
  SetArrayLength(Result,box.X2-box.X1);
  for x := 0 to box.X2-box.X1-1 do
  begin
    SetArrayLength(Result[x],box.Y2-box.Y1);
    for y := 0 to box.Y2-box.Y1-1 do
    begin
      Result[x][y] := GetColor(x+box.X1,y+box.Y1);
    end;
  end;
end;

(*
ValidatePixels
~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure ValidatePixels(Box : TBox; OrigCols : T2DIntegerArray; BoolArr : array of TBoolArray; Tolerance : Integer);

Checks for which pixels, in OrigCols, have changed
beyond the specified tolerance, since the last check

.. note::

    Author: m34tcode
    Last Modified: 4/7/2012
*)
procedure ValidatePixels(Box : TBox; OrigCols : T2DIntegerArray; BoolArr : array of TBoolArray; Tolerance : Integer);
var
  X,Y : Integer;
  NewCols : T2DIntegerArray;
begin
  NewCols := ReturnGridColorsInBox(Box);
  for X := 0 to Box.X2 - Box.X1-1 do
   for Y := 0 to Box.Y2 - Box.Y1-1 do
   begin
     if not BoolArr[x][y] then
       continue;
     boolArr[x][y] := SimilarColors(origCols[x][y],NewCols[x][y],Tolerance);
   end;
end;

(*
InitBoolArr
~~~~~~~~~~~

.. code-block:: pascal

    function InitBoolArr(Width,Height : Integer): array of TBoolArray;

Initializes a 2D boolean array, of dimensions Width x Height to True;

.. note::

    Author: m34tcode
    Last Modified: 4/7/2012
*)
function InitBoolArr(Width,Height : Integer) : array of TBoolArray;
var x,y : Integer;
begin
  SetArrayLength(Result,Width);
  for x := 0 to Width-1 do
  begin
    SetArrayLength(Result[x],Height);
    for Y := 0 to Height-1 do
    begin
      Result[x][y] := True;
    end;
  end;
end;

(*
CountBools
~~~~~~~~~~

.. code-block:: pascal

    function CountBools(BoolArr : Array of TBoolArray; CountTrues : Boolean) : Integer;

Counts all boolean values in the array, equal to CountTrue

.. note::

    Author: m34tcode
    Last Modified: 4/7/2012
*)
function CountBools(BoolArr : Array of TBoolArray; CountTrues : Boolean) : Integer;
var
  X,Y,W,H,C : Integer;
begin
  W := GetArrayLength(BoolArr);
  H := GetArrayLength(BoolArr[0]);
  For X := 0 to W-1 do
    for Y := 0 to H-2 do
      if BoolArr[X][Y] = CountTrues then
        Inc(C);
  Result := C;
end;

(*
PixelShiftOT
~~~~~~~~~~~~

.. code-block:: pascal

    function PixelShiftOT(Box : TBox; ShiftInterval, Repetitions, Tolerance : Integer) : Integer;

Returns an integer equal to the number of pixels,
  within Box, that changed more than tolerance,
  in the time specified by ShiftInterval * Repetitions

.. note::

    Author: m34tcode
    Last Modified: 4/7/2012

Example:

.. code-block:: pascal

    PixelCount := PixelShiftOT(IntToBox(10,10,20,20), 500, 4, 10);
*)
function PixelShiftOT(Box : TBox; ShiftInterval, Repetitions, Tolerance : Integer) : Integer;
var
  I : Integer;
  T,TT : LongInt;
  OrigColArr : T2DIntegerArray;
  BoolArr : array of TBoolArray;
begin
  OrigColArr := ReturnGridColorsInBox(Box);
  BoolArr := initBoolArr(Box.X2-Box.X1,Box.Y2-Box.Y1);
  MarkTime(T);
  MarkTime(TT);
  for I := 0 to Repetitions-1 do
  begin
    if (TimeFromMark(T) > (Repetitions*ShiftInterval)) then
    begin
      srl_Warn('CalcNonStaticPixelCount','Completed only ' + IntToStr(I) + ' of '
        + IntToStr(Repetitions) + '. Try increasing the interval',1);
      break;
    end;
    MarkTime(TT);
    wait(Max(0,ShiftInterval-TimeFromMark(TT)));
    ValidatePixels(Box,OrigColArr,BoolArr,Tolerance);
  end;
  Result := CountBools(BoolArr,False);
end;

(*
AnimatingMulti
~~~~~~~~~~~~~~

.. code-block:: pascal

    function AnimatingMulti(B: TBoxArray; Time, MinCount: Integer): TBooleanArray;

Finds constant animation in boxes B during time T. If PixelShift in box is
smaller than MinCount, it's considered not animating.

.. note::

    Author: marpis
    Last Modified: Nov. 29, 2009

Example:

.. code-block:: pascal

  boolArray := AnimatingMulti(boxArray, 5000, 2000);
    for i := 0 to High(boolArray) do
    isAnimating := boolArray[i];
*)
function AnimatingMulti(B: TBoxArray; Time, MinCount: Integer): TBooleanArray;
var
  Shifts: TIntegerArray;
  TT, I, H: Integer;
begin
  TT := GetSystemTime + Time;
  H := High(B);
  SetLength(Result, H + 1);
  for I := 0 to H do
    Result[I] := True;
  while (GetSystemTime < TT) do
  begin
   Shifts := PixelShiftMulti(B, 50);
   for I := 0 to H do
     if (Shifts[I] <= MinCount) then
       Result[I] := False;
  end;
end;

(*
Animating
~~~~~~~~~

.. code-block:: pascal

    function Animating(B: TBox; Time, MinCount: Integer): Boolean;

Finds constant animation in boxes B during time T. If PixelShift in box is
smaller than MinCount, it's considered not animating.

.. note::

    Author: marpis
    Last Modified: Nov. 29, 2009

Example:

.. code-block:: pascal

  playerAnimating := Animating(box, 5000, 2000);
*)
function Animating(B: TBox; Time, MinCount: Integer): Boolean;
var
  TBA: TBooleanArray;
begin
  TBA := AnimatingMulti([B], Time, MinCount);
  Result := TBA[0];
end;

(*
IsMoving
~~~~~~~~

.. code-block:: pascal

    function IsMoving: Boolean;

Results true if character is moving.

.. note::

    Author: marpis
    Last Modified: Nov. 29, 2009

Example:

.. code-block:: pascal

  PlayerMoving := IsMoving;
*)

function IsMoving: Boolean;
begin
  Result := PixelShift(IntToBox(MMCX-30, MMCY-30, MMCX+30, MMCY+30), 500) > 200;
end;

function Flag: Boolean; forward;

(*
WaitNotMoving
~~~~~~~~~~~~~

.. code-block:: pascal

    function WaitNotMoving(WaitFlag: Boolean): Boolean;

Waits until you arent moving, WaitFlag boolean is there because if you clicked
in an area that isnt walkable the flag will not disappear. Includes a timeout
of 20 seconds for safety.

.. note::

    Author: Ollybest

Example:

.. code-block:: pascal

  if WaitNotMoving(true) then
    writeln('Our player has stopped moving!');

*)
function WaitNotMoving(WaitFlag: Boolean): Boolean;
var
  T: Integer;
begin
  Result := False;
  if not (LoggedIn) then
    Exit;

  MarkTime(T);
  if (WaitFlag) then
    Flag;

  repeat
    Wait(RandomRange(50, 100));
    if not (IsMoving) then
    begin
      Result := True;
      Exit;
    end;
  until (TimeFromMark(T) > 20000);
end;                         
